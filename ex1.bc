|******************************************************************************
|* tdpur2201  0  VRC B50  a  fd01
|* Convert Requisition to RFQ/PO
|* Suresh Basireddy
|* 1997-02-07
|******************************************************************************
|* Main table    : tdpur201 Purchase Requisition Lines
|* Form Type    : Type 4
|* Description    : This session is used to convert all the requisitions which
|*          have been final approved to either PO's or RFQ's. Requisitions
|*          can be converted to PO with or without consolidation.
|******************************************************************************
|******************************************************************************
|*    Rev.No. 5  tdB60asy54  04 Jun 10 djoling
|* Script cleaned by: duutl1200m000 user: djoling
|*    Rev.No. 6  tdB60asy54  15 Jun 10 djoling
|* Additions in Requisitions regarding Workflow.
|*    Rev.No. 8  tdB60asy54  21 Jun 10 djoling
|* .
|*    Rev.No. 10  tdB60asy54  22 Jun 10 hmulder1
|* .
|*    Rev.No. 12  tdB60asy54  28 Jun 10 djoling
|* WRKFL-DJ: Rework Done.
|*    Rev.No. 2  tdB60asy58  28 Sep 10 djoling
|* TCS 800-173647: Delete Requisition after Conversion should be disabled.
|*    Rev.No. 2  tdB60asy59  21 Feb 11 djoling
|* TCS 800-177220: No correct message is shown when requisitions are not
|* converted.
|*    Rev.No. 2  tdB60asy77  24 Mar 14 wnaberma
|* #wn-misc01 Reset error messages moved to session for BDE call.
|*    Rev.No. 3  tdB60asy77  02 Apr 14 nvdknaap
|* SRV-SUBCnk: Subcontracting with material flow in Service. FD: 50707. CD:
|* 50228
|*    Rev.No. 4  tdB60asy77  16 Apr 14 awouters
|* P_RET_ORDaw.  Added Order Type and Series for Return Rejects.
|*    Rev.No. 5  tdB60asy77  28 Apr 14 rnouwt
|* wb.requisition changes added.
|*    Rev.No. 3  tdB60asy82  10 Sep 14 wnaberma
|* #800-198172 Assert message about report group when doing a convert
|* requisition.
|*    Rev.No. 2  tdB60asy83  13 Oct 14 rnouwt
|* cleaned.
|*    Rev.No. 3  tdB6111sy01  11 Feb 15 djoling
|* TCS 800-202232: Subcontracting and RFQ fields still visible when concept /
|* module parameters are turned off.
|*    Rev.No. 2  tdB6114sy01  25 Sep 15 nvdknaap
|* REQ-UXnk: User Experience improvements for Requisitions: Buyer can now be
|* stored on the line and defaulted from Item-Purchase data (1) and project
|* related fields on the line can now be defaulted from the requisition header
|* (2)
|*    Rev.No. 3  tdB6114sy01  07 Oct 15 nvdknaap
|* REQ-UXnk: User Experience improvements for Requisitions: Buyer can now be
|* stored on the line and defaulted from Item-Purchase data (1) and project
|* related fields on the line can now be defaulted from the requisition header
|* (2)
|*    Rev.No. 2  tdB6121sy01  21 Jun 17 hmulder
|* MS-HM - Multisite implementation.
|*    Rev.No. 3  tdB6121sy01  24 Aug 17 hmulder
|* MS-HM - Multisite implementation.
|*    Rev.No. 4  tdB6121sy01  25 Aug 17 hmulder
|* MS-HM - Multisite implementation.
|*    Rev.No. 2  tdB6122sy01  16 Nov 17 hmulder
|* MS-HM - Multi Site implementation.
|*    Rev.No. 3  tdB6122sy01  17 Nov 17 gjweerh
|* Unused linked libraries removed.
|*    Rev.No. 2  tdB6123sy01  12 Dec 17 jbissch2
|* RFQ_JB: Implementation of RFQ Types (#55660).
|*    Rev.No. 3  tdB6123sy01  13 Dec 17 jbissch2
|* RFQ_JB: Implementation of RFQ Types (#55660).
|*    Rev.No. 4  tdB6123sy01  19 Dec 17 jbissch2
|* RFQ_JB: Implementation of RFQ Types (#55660).
|*    Rev.No. 5  tdB6123sy01  12 Jan 18 hmulder
|* VSC - Future errors solved.
|*    Rev.No. 2  tdB6123sy02  05 Feb 18 jbissch2
|* RFQ_ENH_JB: Implementation of RFQ Types (#55660).
|*    Rev.No. 2  tdB6123sy03  28 Feb 18 nvdknaap
|* NK-RFQENH: RFQ Enhanced Line Handling. Total-Detail, Alternatives etc. CD:
|* #55351.
|*    Rev.No. 3  tdB6123sy03  05 Mar 18 nvdknaap
|* NK-RFQENH: RFQ Enhanced Line Handling. Total-Detail, Alternatives etc. CD:
|* #55351.
|*    Rev.No. 2  tdB6123sy04  13 Mar 18 nvdknaap
|* NK-RFQENH: Enhanced Line Handling. Total-Detail, Alternatives etc. CD: #55351
|*    Rev.No. 3  tdB6123sy04  19 Mar 18 nvdknaap
|* NK-RFQENH: Enhanced Line Handling. Total-Detail, Alternatives etc. CD: #55351
|*    Rev.No. 4  tdB6123sy04  21 Mar 18 nvdknaap
|* NK-RFQENH: Enhanced Line Handling. Total-Detail, Alternatives etc. CD: #55351
|*    Rev.No. 5  tdB6123sy04  29 Mar 18 djoling
|* TCS 800-223912: Add to existing RFQ no correctly initialized when Enhanced
|* Line handling is not implemented.
|*    Rev.No. 2  tdB6123sy05  19 Apr 18 jbissch2
|* RFQ_ENH3_JB: Replace Subline by Alternative (#55660).
|*    Rev.No. 3  tdB6123sy05  23 Apr 18 djoling
|* RFQ5-DJ: Implementation of new RFQ enhancements.
|*    Rev.No. 5  tdB61M22  29 May 18 bboot
|* Checked in by session ttadv6265m000, component copied from VRC B61  23 sy05
|*    Rev.No. 2  tdB6124sy01  29 Jun 18 hmulder
|* 1994623-HM - Additional implementation for RFQ enhancement.
|******************************************************************************
|*
|* SOL 251000, Maria Proos, 2010-10-26
|* Series number saved as default is not displayed once session is started.
|* (Infor365 - 289568)
|*
|* SOL 1013991, Nicolette Kroon, 2011-05-09
|* No cost order type allowed when converting requisition (Infor365 310983).
|*
|* SOL 1186893, Niek Greven, 2012-10-25
|* When requisitions are converted to purchase order via the specific option,
|* different purchase orders are generated.
|* infor365, defect 371656
|*
|* SOL 1367369, Nicolette Kroon, 2013-01-16
|* Requisitions are commingled when they are converted to Purchase Orders
|* (InforXtreme 385147).
|*
|* SOL REQ_PRM-DJ, Dirk Joling, 2015-01-26
|* Implementation of Requisition (concept/module) parameter (PD: 53560).
|*
|* SOL WF-REQ-HM, Hans Mulder, 2015-05-08
|* Workflow Document Authorization for Requisitions.
|*
|* SOL 2029575, Richard Buitenhuis, 2018-11-28
|* Requisition conversion should be possible upto 999 lines without errors
|* coming up. (infor Support - DF 665118)
|*
|* SOL 2062611, Nicolette Kroon, 2019-05-14
|* Not able to convert Purchase Requisition Line to Purchase Order from
|* Purchase Requisition Conversion Workbench session when Commingle is not
|* selected (InforSupport 683745).
|******************************************************************************

|****************************** declaration section ***************************

declaration:
#ident "@(#)tdpur2201  tdB61M25  nlbalappsmnt1  Rev.No. 2  14 May 19  nkroon"
#ident "@(#)tdpur2201  tdB6125sy01  NLBAWAPPSDEV2  Rev.No. 2  28 Nov 18  rbuitenh"
#ident "@(#)tdpur2201  tdB6124sy01  NLBAWAPPSDEV2  Rev.No. 2  29 Jun 18  hmulder"

|++++++    tables +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    table    ttccom001    |* Employees - General
    table    ttcmcs050    |* First Free Numbers
    table    ttcmcs051    |* Number Groups
    table    ttdpur043    |* User Profiles (Purchase)
    table    ttdpur096    |* RFQ Types
    table    ttdpur100    |* Requests for Quotation
    table    ttdpur200    |* Purchase Requisitions
    table    ttdpur201    |* Purchase Requisition Lines

|************************ External variables **********************************

    |* zoom variables
    extern    domain    tcemno        xi.approver
    extern    domain    tccwoc        xi.appr.department
    extern        boolean        xi.filter.approvers
    extern        boolean        xi.filter.appr.departments
    extern    domain    tcrqno        xi.requisition
    extern    domain    tcpono        xi.requisition.line

|************************ form variables **************************************

    |* Selection Range
    extern    domain    tcemno        requester.f fixed
    extern    domain    tcemno        requester.t fixed
    extern    domain    tcemno        approver.f fixed
    extern    domain    tcemno        approver.t fixed
    extern    domain    tcemno        buyer.f fixed
    extern    domain    tcemno        buyer.t fixed
    extern    domain    tccwoc        appr.dept.f fixed
    extern    domain    tccwoc        appr.dept.t fixed
    extern    domain    tcrqno            requisition.f fixed
    extern    domain    tcrqno            requisition.t fixed
    extern    domain    tcpono        req.line.f
    extern    domain    tcpono        req.line.t
    extern    domain    tcdate        req.date.f
    extern    domain    tcdate        req.date.t

    |* Purchase Order
    extern     domain     tcnrgr          ordr.number.group
    extern    domain    tcdsca        order.group.desc
    extern     domain     tcseri          order.series fixed
    extern     domain     tcdsca          order.series.desc
    extern     domain     tccotp         order.type fixed
    extern     domain     tcdsca         order.type.desc
    extern    domain    tcseri        subc.order.series fixed
    extern     domain     tcdsca          subc.ord.ser.desc
    extern     domain     tccotp         subc.order.type fixed
    extern    domain    tcdsca        subc.ord.type.dsc
    extern    domain    tcseri        serv.subc.series fixed
    extern     domain     tcdsca          servsubc.ser.desc
    extern     domain     tccotp         serv.subc.otype fixed
    extern    domain    tcdsca        servsubc.otyp.dsc

    |* RFQ
    extern     domain     tcnrgr          rfq.number.group
    extern    domain    tcdsca        rfq.group.desc
    extern    domain    tcseri        rfq.series fixed
    extern     domain     tcdsca          rfq.series.desc
    extern    domain    tcrfq.type    rfq.type fixed
    extern     domain     tcdsca          rfq.type.desc
    extern    domain    tcyesno        f.add.to.existing
    extern    domain    tcqono        f.existing.rfq
    extern    domain    tcdsca        f.exist.rfq.desc

    |* Settings
    extern    domain    tcyesno        commingle
    extern    domain    tcyesno        del.req.after.con
    extern    domain    tcyesno        currency.of.bp
    extern    domain    tcyesno        new.pric.disc

    |* Options
    extern    domain    tcyesno        success.report
    extern    domain    tcyesno        error.report

|++++++    specific record selection +++++++++++++++++++++++++++++++++++++++++++++

    extern     domain    tcrqno        rqno.select(10)
    extern    domain    tcrqno        wb.rqno.select(1)    based
    extern    domain    tcmcs.long    wb.no.select

        domain    tcyesno        g.spec.rqno

|************************ report variables ************************************

    extern        boolean        splitted.mess
    extern    domain    tcmcs.s132m    err.mess mb
    extern    domain    tcorno        gen.orno
    extern    domain    tcpono        gen.pono
    extern    domain    tcpono        gen.sqnb

|************************ program variables ***********************************

            boolean        g.record.converted
            boolean        g.process.stopped

|************************ includes ********************************************

    #include <bic_dam>    |* DAL Support defines (tools)
    #include "itcmcs2000"    |* DAL(-GUI) Support Defines
    #include "itcmcs2020"    |* UI Engine (for UI template tcmcstempl_ui)

|****************************** program section *******************************

before.program:
    if handle.reading.before.program() <> 0 then
        show.dal.messages()
        stop()
    endif
    handle.initialization.before.program()
    handle.session.interface()

before.display.object:
    handle.disabling.input.fields()

|****************************** form section **********************************

after.form.read:
    handle.invisibility.fields()
    handle.invisibility.form.commands()

group.1:
init.group:
    if background and
       (wb.no.select > 0 or sel.parent.num.selected() ) > 0 then
        handle.global.form.variables()
    endif

    handle.purchase.form.variables()
    get.screen.defaults()

|****************************** choice section ********************************

choice.cont.process:
before.choice:
    check.all.input()
on.choice:
    execute(print.data)

choice.print.data:
before.choice:
    if success.report = tcyesno.yes then
        reportgrp = 1
    else
         if error.report = tcyesno.yes then
             reportgrp = 2
         endif
     endif
on.choice:
    if not handle.check.open.report() then
        choice.again()
    endif
    START.DLL.UI(convert.requisitions, IFERRABT, "tdpurs2201.1")
        #pragma used message tdpurs2201.1
        |* Convert Progress - Requisitions
after.choice:
    close.open.reports()
    handle.closing.message.in.statusbar()
    prcm.notify("tdpur200")

|****************************** field section *********************************
field.all:
when.field.changes:
    if eng.fieldname.without.segment() then
        refresh.field.dependent.display.fields()
        handle.disabling.input.fields()
    endif

field.requester.f:
before.zoom:
    requester.handle.before.zoom(requester.f)
when.field.changes:
    requester.t = requester.f
    display("requester.t")

field.requester.t:
before.zoom:
    requester.handle.before.zoom(requester.t)

field.approver.f:
before.zoom:
    approver.handle.before.zoom(approver.f)
when.field.changes:
    approver.t = approver.f
    display("approver.t")

field.approver.t:
before.zoom:
    approver.handle.before.zoom(    approver.t)

field.buyer.f:
before.zoom:
    buyer.handle.before.zoom(buyer.f)
when.field.changes:
    buyer.t = buyer.f
    display("buyer.t")

field.appr.dept.f:
before.zoom:
    appr.dept.handle.before.zoom(    appr.dept.f)
when.field.changes:
    appr.dept.t = appr.dept.f
    display("appr.dept.t")

field.appr.dept.t:
before.zoom:
    appr.dept.handle.before.zoom(appr.dept.t)

field.requisition.f:
before.zoom:
    requisition.handle.before.zoom(requisition.f)
when.field.changes:
    requisition.t = requisition.f
    display("requisition.t")

field.requisition.t:
before.zoom:
    requisition.handle.before.zoom(    requisition.t)

field.req.line.f:
before.zoom:
    req.line.handle.before.zoom(    requisition.f,
                    req.line.f)
when.field.changes:
    req.line.t = req.line.f
    display("req.line.t")

field.req.line.t:
before.zoom:
    req.line.handle.before.zoom(    requisition.t,
                    req.line.t)

field.req.date.f:
when.field.changes:
    req.date.t = req.date.f
    display("req.date.t")

field.req.date.t:
check.input:
    handle.check.input.req.date.t(    req.date.f,
                    req.date.t)

field.order.series:
before.zoom:
    tcmcs050.nrgr = ordr.number.group
check.input:
    handle.check.input.order.series(order.series)
when.field.changes:
    order.series.desc = get.series.description(
                    ordr.number.group,
                    order.series)

field.order.type:
selection.filter:
    order.type.handle.selection.filter(
                    tcyesno.no)    |* subcontracting
check.input:
    handle.check.input.order.type(    order.type,
                    tcyesno.no)
when.field.changes:
    order.type.desc = get.order.type.description(
                    order.type)

|****************************** function section ******************************
functions:

function long handle.reading.before.program()
{
    if not tdgen.dll0000.requisitions.is.implemented(
                    true) then    |* set message
        return(DALHOOKERROR)
    endif

    tdpur.dll0400.read.user.profile()
    return(0)
}

function void handle.initialization.before.program()
{
    f.add.to.existing = tcyesno.no
}

function void handle.session.interface()
{
    handle.import.from.workbench()
    handle.adding.parent.tables()
}

function void handle.adding.parent.tables()
{
            boolean        dummy.bool

    dummy.bool = sel.add.parent.tables(
                    7,
                    "tdpur201")
}

|****************************** end of script *********************************
